;ToolTypes library (C)1994 Reflective Images
;Example program
;By Stephen McNamara
;Please feel free to use any part of the program
; in whatever way you feel like

;PLEASE CD into the directory that holds this basic file since we
;need to be able to load the info file: ToolTypes.bb2.info.

;Startup the program up
NoCli
WBStartup
WbToScreen 0
ShowScreen 0
Use Screen 0

;Get a window to print into!
Window 0,0,11,640,100,$100a,"ToolTypes Test Program",2,1
Use Window 0

;our filename.  You should note that the filename has .info added
;automatically to it when used in the GET/PUTDiskObject commands.
filename$="ToolTypes.bb2"

;get a disk object and return success (you don't have to get success back)
;you could just use: GetIconObject filename$
suc.l=GetIconObject(filename$)

;check to see if it was loaded okay
If suc=0
  NPrint "Couldn't open file: "+filename$
  NPrint "Did you CD into the directory?"
  Repeat
    ev.l=WaitEvent
  Until ev=$200
  End
End If

;Get some tooltype information
NPrint "Tooltype ICONX has value "+FindToolValue("ICONX")
NPrint "Tooltype ICONY has value "+FindToolValue("ICONY")

;check for tool value match
If MatchToolValue("LANGUAGE","ENGLISH") AND MatchToolValue("LANGUAGE","ENGLISH")
  NPrint "Tooltype LANGUAGE has values ENGLISH and FRENCH"
EndIf

;now change some tooltypes
suc.l=SetToolValue("ICONX","150")
NPrint "Change tooltype ICONX to 150 (-1=success): "+Str$(suc)
suc.l=SetToolValue("ICONY","100")
NPrint "Change tooltype ICONY to 100 (-1=success): "+Str$(suc)

;create a new tooltype
suc.l=NewToolType("ICONVALUE","CLOSED",False)
NPrint "Create tooltype ICONTYPE with value CLOSED and switch OFF (-1=success): "+Str$(suc)

;save out our icon information to RAM:
suc.l=PutIconObject("RAM:"+filename$)
NPrint "Saved okay? (-1=success): "+Str$(suc)

NPrint "Close this window to end!"

Repeat
  ev.l=WaitEvent
Until ev=$200

Free Window 0
Free Screen 0
End
