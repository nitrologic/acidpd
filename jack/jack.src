
For k=1 To 100
VWait:CloseWorkBench_
Next

BitMap 0,640+16,400,4
Cls
BitMap 1,640+16,400,4  ;play area
Cls

BitMap 2,320,9,3    ;score bar  8 colours!!
Cls

Dim map.w(40,25)
Dim lev.w(40,25)

Dim px.q(80),py.q(80),pvx.q(80),pvy.q(80)

Buffer 0,768*10
Buffer 1,768*10


NEWTYPE .obj

here.q
x.q
y.q
vx.q
vy.q
typ.w
fr.q
hi.w
lo.w
dir.w
ping.w
cnt.w
spd.w
life.w

End NEWTYPE

;object types
bit=0       ;bit of block exploding..
coin.w=1
faller.w=2


LoadBlitzFont 0,"jump.font"

;********************************************************
.SETUP
BLITZ

BitMapInput
BitMapOutput 0
Use BlitzFont 0

BlitzKeys On

Mouse On
Slice 0,50,320,190,$fff8,4,8,32,640+16,640+16     ;main playing area

Slice 1,40,320,8,$fff0,3,0,8,320,320        ;score bar..
Show 2,0,0
Use BitMap 2
BitMapOutput 2
Use BlitzFont 0
RGB 0,0,0,0
RGB 1,15,15,15
RGB 2,15,0,0
RGB 3,0,15,0
RGB 4,0,7,15
RGB 5,15,0,15
RGB 6,15,15,0
RGB 7,14,9,0
score.l=0
lives.w=3
time.q=0
coins.w=0
Use BitMap 2
BitMapOutput 2
Locate 0,0
Print "                                       "
Locate 0,0
Colour 2
Print "SCORE:"
Locate 6,0
Colour 1
Print score   ;000000
Locate 12,0
Colour 3
Print "LIVES:"
Locate 18,0
Colour 1
If lives<=9
    Print lives," "     ;00
Else
    Print "9+"
EndIf
Locate 21,0
Colour 4
Print "COINS:"
Locate 27,0
Colour 1
Print coins     ;000
Locate 31,0
Colour 5
Print "TIME:"
Locate 36,0
Colour 1
Print Int(time) ;000
Colour 1

Dim objects.obj(16)
Use Slice 0

Function findobject{}
Shared objects()
    f=-1
    For o=0 To 20
        If objects(o)\here=0
            f=o:Pop For:Goto aaa
        EndIf
    Next
aaa
    Function Return f
End Function

DisplayOff
QAMIGA
LoadShapes 0,"jack/shapes.shp"
LoadShapes 100,"jack/coin.shp"
LoadPalette 0,"jack/shapes.iff"
LoadShapes 199,"jack/go.shp"


LoadSound 0,"jack/smash.sfx"
LoadSound 1,"jack/jump.sfx"
LoadSound 2,"jack/bell.sfx"
LoadSound 3,"jack/hithead.sfx"
LoadSound 4,"jack/faller.sfx"
LoadSound 5,"jack/gotenough.sfx"
LoadSound 6,"jack/coinget.sfx"
LoadSound 7,"jack/cash.sfx"
LoadSound 8,"jack/freeman.sfx"
LoadSound 9,"jack/GameOver.sfx"
LoadSound 10,"jack/LoseLife.sfx"
LoadSound 11,"jack/Complete.sfx"

Volume 15,64,64,64,64

BLITZ
DisplayOn:Show 0
Use Palette 0
;********************************************************
.FRONTEND

FlushBuffer 0
FlushBuffer 1

Use Slice 0
Use BitMap 0
BitMapOutput 0
Show 0,0,0
Cls

Boxf 0,318,90,328,0
Colour 1
Locate 0,40
Print " PRESS FIRE "
For y=318 To 318+10
    For x=0  To 88
        If Point(x,y)>0 AND Point(x,y)<>12
            If Point(x-1,y)=0
                Plot x-1,y,12
            EndIf
            If Point(x+1,y)=0
                Plot x+1,y,12
            EndIf
            If Point(x,y-1)=0
                Plot x,y-1,12
            EndIf
            If Point(x,y+1)=0
                Plot x,y+1,12
            EndIf
        EndIf
    Next
Next
GetaShape 190,0,318,90,10
Cls

flsh.q=0
goflsh.w=0

DisplayOff
QAMIGA
LoadBitMap 0,"jack/title.iff",0
BLITZ
Use Palette 0
DisplayOn
Show 0,0,0
CopyBitMap 0,1


SetInt 5

    If goflsh=1

        Show db,0,0
        db=1-db
        Use BitMap db
        UnBuffer db

        If flsh<.6 Then BBlit db,190,112,180
        If flsh>0
            flsh=flsh-.02
        Else
            flsh=1
        EndIf

    EndIf

End SetInt


startfe:
flsh.q=0

;show screens etc.. (0 and 1)


db=0
goflsh=1
For l=0 To 200
    VWait

    If Joyb(1)<>0 Then Pop For:Goto START
Next
UnBuffer 0
UnBuffer 1

Goto startfe

;********************************************************
.START          ;start a new game...
ClrInt 5

QAMIGA
LoadPalette 0,"jack/shapes.iff"
BLITZ
Use Palette 0

f$=""
level=1
score.l=0
lives.w=3
time.q=0
coins.w=0
nextfree.l=10000

lf$=""

;----------------------------------------------------
.STARTLEVEL
editor=0

Use BitMap 0
Cls
Use BitMap 1
Cls

rslvl:
a$=Str$(level)
f$="jack/map"+a$+".bin"

If lf$<>f$
;load level..
    DisplayOff
    QAMIGA
    h.l=Open_(f$,1005)
    If h.l=0
        level=1
        f$="jack/map1.bin"
        h.l=Open_(f$,1005)
    EndIf
    For y=0 To 24
        For x=0 To 39
            a.b=0
            c.l=Read_(h,&a,1)
            lev(x,y)=a
        Next
    Next
    c.l=Close_(h)
    BLITZ
    DisplayOn:Show 0
    lf$=f$
EndIf

For y=0 To 24
    For x=0 To 39
        map(x,y)=lev(x,y)
    Next
Next

coins.w=0

numcoin=0
;figure out # of coins..
For y=0 To 24
    For x=0 To 39
        If map(x,y)=>20 AND map(x,y)<=23 Then numcoin+1
    Next
Next

For l=0 To 20
    objects(l)\here=0
Next


;----------------------------------------------------
.STARTLIFE
editor=0

If lives=0 Then Goto GAMEOVER

ux=1*16:uy=23*16
sx=QLimit(ux-160,0,320)
sy=QLimit(uy-100,0,210)

vx=0:vy=0
jump=0

Gosub drawmap

time=500        ;!!!!!!!!!!!!!!!!!
warn=0

Use BitMap 2
BitMapOutput 2
Locate 0,0
Print "                                       "
Locate 0,0
Colour 2
Print "SCORE:"
Locate 6,0
Colour 1
Print score     ;000000

Locate 12,0
Colour 3
Print "LIVES:"
Locate 18,0
Colour 1
If lives<=9
    Print lives," "     ;00
Else
    Print "9+"
EndIf

Locate 21,0
Colour 4
Print "COINS:"
Locate 27,0
Colour 1
If Int((coins/numcoins)*100)=>75 Then Colour 2
Print coins     ;000

Locate 31,0
Colour 5
Print "TIME:"
Locate 36,0
Colour 1
If time<100
    Colour 7
EndIf
Print Int(time) ;000
Colour 1

Use Slice 0

gx=320
gy=280
tt=0

FlushBuffer 0
FlushBuffer 1

;********************************************************
GoFrame:
VWait
Show db,sx,sy
db=1-db
Use BitMap db
UnBuffer db

If gx>140 Then gx=gx-4
If gx=140 Then tt+1
If tt>40
    If gy>170
        gy-6
    Else
        Goto EndGFrame
    EndIf
EndIf

BBlit db,199,gx,gy

Goto GoFrame

EndGFrame:
UnBuffer 0
UnBuffer 1

breqx=-1    ;no req!!
breqx2=-1    ;no req!!

ufr.q=0
uhi=4
ulo=0
ucnt=0


;********************************************************
.FRAME
editor=0
VWait
Show db,sx,sy
db=1-db
Use BitMap db
UnBuffer db

; check for last frame req's
If breqx<>-1
    If breqn=>0
        Blit breqn,breqx,breqy
    Else
        Boxf breqx,breqy,breqx+15,breqy+15,0
    EndIf
    breqx=-1
EndIf
If breqx2<>-1
    Blit breqn2,breqx2,breqy2
    breqx2=-1
EndIf

I$=Inkey$

;cheat!!
If I$="`" Then coins=numcoin:Sound 5,4:level+1:levelf=1:Goto ENDLEVEL

If Asc(I$)=27
    lives-1:If lives=0 Then Goto GAMEOVER
    Sound 10,4
    Goto STARTLEVEL
EndIf

If I$=" " Then Goto EDITOR

If I$="p"
    VWait 10
    paa:VWait 5
    a$=""
    a$=Inkey$
    If LCase$(a$)<>"p" Then Goto paa
EndIf

Use BitMap 2
BitMapOutput 2
Locate 36,0
Colour 1
If time<100
    Colour 7
EndIf
Print Int(time)," "
Colour 1

Use BitMap db

If time>0 Then time-.05     ;20 fr=1 count
If time<0 Then time=0

If time=0
    If lives>1 Then Sound 10,4
    For l=0 To 70
        px(l)=ux+Int(Rnd(16))
        py(l)=uy+Int(Rnd(16))
        pvx(l)=Rnd(7)-3
        pvy(l)=-(4+Rnd(3))
    Next
    Use BitMap 1-db
    For dd=0 To 80
        For l=0 To 70
            px(l)+pvx(l)
            py(l)+pvy(l)
            pvy(l)+.2
            Plot px(l),py(l),Int(Rnd(14))+1
        Next
    Next
    lives-1:Goto STARTLIFE
EndIf

If time>0 AND time<100
    If Int(time/10)*10=time AND warn=0
        Sound 2,4:warn=20
    Else
        If warn>0 Then warn-1
    EndIf
EndIf

;free man??
If score=>nextfree
    Sound 8,4
    lives+1
    nextfree+10000
    Use BitMap 2
    BitMapOutput 2
    Locate 18,0
    Colour 1
    If lives<=9
        Print lives," "     ;00
    Else
        Print "9+"
    EndIf
    Use BitMap db
    BitMapOutput 0
EndIf

oux=ux
ouy=uy
osx=sx
osy=sy

jx=Joyx(1)
jy=Joyy(1)
jb=Joyb(1)

If jump=0
    vx=QLimit(vx+(jx/2),-4,4)
Else
    vx=QLimit(vx+(jx/4),-4,4)
    lastmax=vx
EndIf

If jx=0
    If vx>0
        vx=QLimit(vx-.2,0,4)
    EndIf
    If vx<0
        vx=QLimit(vx+.2,-4,0)
    EndIf

    uhi=ulo

EndIf

If jb=1 AND jump=0
    Sound 1,0:Sound 1,2
    vy=-6
    jump=1
    lastmax=vx
    If ufr<5 OR ufr=10
        uhi=10
        ulo=10
    Else
        uhi=11
        ulo=11
    EndIf
EndIf

If jump=1
    ovy=Sgn(vy)
    vy=QLimit(vy+.4,-6,7)
    If Sgn(vy)<>ovy Then falldist=0
EndIf

If vx>0 AND jump=0
    uhi=4
    ulo=0
EndIf
If vx<0 AND jump=0
    uhi=9
    ulo=5
EndIf

ux=QLimit(ux+vx,0,640-16)
uy=QLimit(uy+vy,0,400-16)

sx=QLimit(ux-160,0,320)
sy=QLimit(uy-100,0,210)

;vx=velocity x
;vy=velocity y
;
tlx=Int(ux/16):tly=Int(uy/16)
trx=Int((ux+ShapeWidth(ufr)-1)/16):try=Int(uy/16)
;
dd=ShapeWidth(ufr)/2
;
blx=Int((ux+dd)/16):bly=Int((uy+17)/16)      ;below you
blxa=Int((ux+dd)/16):blya=Int((uy-1)/16)    ;above you

blkl=map(tlx,tly)
blkr=map(trx,try)

blkb=map(blx,bly)
blka=map(blxa,blya)

If vy=0
    If blkb<=-1
        vy=QLimit(vy+.4,-6,6)
        jump=1:falldist=0
    EndIf
EndIf

If vx>0
    If blkr>-1
        vx=0
        ux=oux ;Int((ux)/16)*16          ;+8
    EndIf
EndIf

If vx<0
    If blkl>-1
        vx=0
        ux=oux ;Int((ux+15)/16)*16       ;+8
    EndIf
EndIf

If vy>0 AND blkb>-1         ;coming down..
    vy=0
    uy=Int((uy+8)/16)*16
    jump=0
    If Abs(lastmax)>.4
        vx=lastmax
    Else
        vx=.4
    EndIf
    lastmax=0

    If falldist>180
        If lives>1 Then Sound 10,4
        For l=0 To 70
            px(l)=ux+Int(Rnd(16))
            py(l)=uy+Int(Rnd(16))
            pvx(l)=Rnd(7)-3
            pvy(l)=-(4+Rnd(3))
        Next
        Use BitMap 1-db
        For dd=0 To 80
            For l=0 To 70
                px(l)+pvx(l)
                py(l)+pvy(l)
                pvy(l)+.2
                Plot px(l),py(l),Int(Rnd(14))+1
            Next
        Next
        lives-1:Goto STARTLIFE
    EndIf

    If blkb=>40 AND blkb<=43    ;spikes!!
        If lives>1 Then Sound 10,4
        For l=0 To 70
            px(l)=ux+Int(Rnd(16))
            py(l)=uy+Int(Rnd(16))
            pvx(l)=Rnd(7)-3
            pvy(l)=-(4+Rnd(3))
        Next
        Use BitMap 1-db
        For dd=0 To 80
            For l=0 To 70
                px(l)+pvx(l)
                py(l)+pvy(l)
                pvy(l)+.2
                Plot px(l),py(l),Int(Rnd(14))+1
            Next
        Next
        lives-1:Goto STARTLIFE
    EndIf

    If blkb=>36 AND blkb<=39
        percent.q=(coins/numcoin)*100
        If percent>75
            level+1:levelf=1    ;we added one!
            Goto ENDLEVEL
        Else
            ;reset level...(lose life!)
            lives-1:If lives=0 Then Goto GAMEOVER
            ;no sfx here, its in endlevel!!
            levelf=0            ;we didn't add one
            Goto ENDLEVEL
        EndIf
    EndIf

    falldist=0
Else
    falldist+vy
EndIf

If vy<0 AND blka<>-1        ;going up!!!!!
    vy=0:falldist=0
    uy=Int(uy/16)*16

    If blka<-1              ;show it if hidden..
        Sound 3,1
        map(blxa,blya)=-map(blxa,blya)
        Blit map(blxa,blya),blxa*16,blya*16
        breqx=blxa*16           ;do it next frame as well!
        breqy=blya*16
        breqn=Abs(map(blxa,blya))

        score+100
        BitMapOutput 2
        Use BitMap 2
        Locate 6,0
        Colour 1
        Print score     ;000000
        BitMapOutput 0
        Use BitMap db

        Goto checknomore
    EndIf


    If blka=>20 AND blka<=23        ;question mark coin block
        dd=map(blxa,blya)+4
        map(blxa,blya)=dd    ;change to dead block..
        Blit dd,blxa*16,blya*16  ;0=dead block
        breqx=blxa*16           ;do it next frame as well!
        breqy=blya*16
        breqn=dd

        coins+1
        Use BitMap 2
        dz=Int((coins/numcoin)*100)
        If dz=75
            Sound 5,1
        Else
            Sound 6,1
        EndIf

        If dz<75
            Colour 1
        Else
            Colour 2
        EndIf
        Locate 27,0
        Print coins     ;000

        score+50
        Locate 6,0
        Colour 1
        Print score     ;000000

        Use BitMap db
        Colour 1
        o=findobject{}
        objects(o)\here=1
        objects(o)\typ=coin
        objects(o)\x=blxa*16+8
        objects(o)\y=blya*16-16
        objects(o)\vx=0
        objects(o)\vy=-4
        objects(o)\life=0
        objects(o)\fr=100
        objects(o)\cnt=0

        Goto checknomore

    EndIf

    If blka=>16 AND blka<=19           ;smashable block..
        Sound 0,1
        dd=map(blxa,blya)-4
        map(blxa,blya)=-1    ;change to nothing
        Boxf blxa*16,blya*16,blxa*16+15,blya*16+15,0
        breqx=blxa*16           ;do it next frame as well!
        breqy=blya*16
        breqn=-1                ;-1=boxf 0

        o=findobject{}
        objects(o)\here=1
        objects(o)\typ=bit
        objects(o)\x=blxa*16
        objects(o)\y=blya*16
        objects(o)\vx=-1
        objects(o)\vy=-5
        objects(o)\life=0
        objects(o)\fr=dd

        o=findobject{}
        objects(o)\here=1
        objects(o)\typ=bit
        objects(o)\x=blxa*16+8
        objects(o)\y=blya*16
        objects(o)\vx=1
        objects(o)\vy=-5
        objects(o)\life=0
        objects(o)\fr=dd

        o=findobject{}
        objects(o)\here=1
        objects(o)\typ=bit
        objects(o)\x=blxa*16
        objects(o)\y=blya*16+8
        objects(o)\vx=-.6
        objects(o)\vy=-4
        objects(o)\life=0
        objects(o)\fr=dd

        o=findobject{}
        objects(o)\here=1
        objects(o)\typ=bit
        objects(o)\x=blxa*16+8
        objects(o)\y=blya*16+8
        objects(o)\vx=.6
        objects(o)\vy=-4
        objects(o)\life=0
        objects(o)\fr=dd

        score+50
        BitMapOutput 2
        Use BitMap 2
        Locate 6,0
        Colour 1
        Print score     ;000000
        BitMapOutput 0
        Use BitMap db

        Goto checknomore
    EndIf


    If blka=>32 AND blka<=35    ;faller
        Sound 4,1
        dd=map(blxa,blya)
        map(blxa,blya)=-1
        Boxf blxa*16,blya*16,blxa*16+15,blya*16+15,0
        breqx=blxa*16           ;do it next frame as well!
        breqy=blya*16
        breqn=-1
        o=findobject{}
        objects(o)\here=1
        objects(o)\typ=faller
        objects(o)\x=blxa*16
        objects(o)\y=blya*16
        objects(o)\life=0
        objects(o)\fr=dd

        score+50
        BitMapOutput 2
        Use BitMap 2
        Locate 6,0
        Colour 1
        Print score     ;000000
        BitMapOutput 0
        Use BitMap db

        Goto checknomore
    EndIf


    Sound 3,1

checknomore:    ;only check 1 per frame
EndIf
;---------------------------------------------------------
; update objects

For o=0 To 20
    If objects(o)\here=1

        If objects(o)\typ=bit
            objects(o)\x=objects(o)\x+objects(o)\vx
            objects(o)\y=objects(o)\y+objects(o)\vy
            objects(o)\vy+.4
            objects(o)\life+1
            If objects(o)\life<60
                If objects(o)\x>16 AND objects(o)\x<640-16 AND objects(o)\y>16 AND objects(o)\y<400-16
                    BBlit db,objects(o)\fr,objects(o)\x,objects(o)\y
                EndIf
            Else
                objects(o)\here=0
            EndIf
        EndIf

        If objects(o)\typ=coin

            objects(o)\cnt+1
            If objects(o)\cnt>3
                objects(o)\cnt=0
                objects(o)\fr=QWrap(objects(o)\fr+1,100,105)
            EndIf

            objects(o)\y=objects(o)\y+objects(o)\vy
            objects(o)\vy+.4
            objects(o)\life+1
            If objects(o)\life<40
                If objects(o)\x>16 AND objects(o)\x<640-16 AND objects(o)\y>16 AND objects(o)\y<400-16
                    BBlit db,objects(o)\fr,objects(o)\x,objects(o)\y
                EndIf
            Else
                objects(o)\here=0
            EndIf
        EndIf


        If objects(o)\typ=faller
            If objects(o)\life>20
                objects(o)\y+2
                mox=Int(objects(o)\x/16)
                moy=Int((objects(o)\y+16)/16)
                If map(mox,moy)>-1
                    objects(o)\here=0
                    map(mox,moy-1)=objects(o)\fr
                    Blit objects(o)\fr,mox*16,moy*16-16
                    breqx2=mox*16
                    breqy2=moy*16-16
                    breqn2=objects(o)\fr
                Else
                    BBlit db,objects(o)\fr,objects(o)\x,objects(o)\y
                EndIf
            Else
                BBlit db,objects(o)\fr,objects(o)\x,objects(o)\y
                objects(o)\life+1
            EndIf
        EndIf

    EndIf
Next


;---------------------------------------------------------

ucnt+1
If ucnt>4
    ucnt=0
    ufr=ufr+1
    If ufr<ulo Then ufr=ulo
    If ufr>uhi Then ufr=ulo
EndIf

BBlit db,ufr,ux,uy

sx=QLimit(ux-160,0,320)
sy=QLimit(uy-100,0,210)

If Joyb(0)<>0 Then End
Goto FRAME
;********************************************************
.ENDLEVEL

lastlevel=level-levelf

UnBuffer 0
UnBuffer 1

xs=Sgn(0-sx)*2
ys=Sgn(210-sy)*2

sx=Int(Int(sx)/2)*2
sy=Int((sy)/2)*2

Repeat
    VWait
    Show 0,sx,sy
    f=0
    If sx<>0 Then sx=sx+xs:f=-1
    If sy<>210 Then sy=sy+ys:f=-1

    If f<>0
        f=0
        If sx<>0 Then sx=sx+xs:f=-1
        If sy<>210 Then sy=sy+ys:f=-1
    EndIf

Until f=0
VWait
Show 0,0,210

Use BitMap 0
Boxf 0,0,200,180,0

BitMapOutput 0

If levelf=1
    timebon=Int(time/10)*100
    mul=20:If coins=numcoin Then mul=50
    coinbon=coins*mul

    Colour 1
    Locate 0,0
    NPrint "STAGE ",lastlevel," CLEAR!"
    NPrint ""
    If timebon>0
        Colour 2
        NPrint "TIME BONUS: "
    Else
        Colour 9
        NPrint "NO TIME BONUS!"
    EndIf
    Colour 2
    NPrint "COIN BONUS: "
    If coins=numcoin Then Colour 14:NPrint "":NPrint "ALL COINS COLLECTED!"
    Sound 11,4
Else
    Colour 1
    Locate 0,0
    NPrint "YOU DID NOT COLLECT"
    NPrint "ENOUGH COINS."
    NPrint "TRY AGAIN."
    Sound 10,4
EndIf

;outline in black(10)..

GetaShape 190,0,0,160,50    ;shift it down and along!!
Boxf 0,0,200,180,0
Blit 190,1,1

For y=1 To 49
    For x=1  To 158
        If Point(x,y)>0 AND Point(x,y)<>10
            If Point(x-1,y)=0
                Plot x-1,y,10
            EndIf
            If Point(x+1,y)=0
                Plot x+1,y,10
            EndIf
            If Point(x,y-1)=0
                Plot x,y-1,10
            EndIf
            If Point(x,y+1)=0
                Plot x,y+1,10
            EndIf
        EndIf
    Next
Next

;print in un-outlined scores if we need to..
If levelf=1
    If timebon>0
        Locate 12,2
        Colour 1:NPrint timebon
    EndIf
    Locate 12,3
    Colour 1:NPrint coinbon
EndIf

GetaShape 190,0,0,160,50


Colour 1
For x=320 To 96 Step -4
    VWait
    Show db,0,210
    db=1-db
    Use BitMap db
    UnBuffer db

    BBlit db,190,x,272
Next

If levelf=0 Then Goto doneel

sfx=0
Repeat
    VWait
    f=0
    If timebon>0
        If sfx=0 Then Sound 2,2:sfx=3
        timebon-100
        score+100
        Use BitMap 2
        BitMapOutput 2
        Locate 6,0
        Print score
        Use BitMap 1-db
        BitMapOutput 1-db
        Locate 24,36
        Print timebon,"  "
        f=1
    EndIf

    If coinbon>0
        If sfx=0 Then Sound 2,2:sfx=3
        coinbon-10
        score+10
        Use BitMap 2
        BitMapOutput 2
        Locate 6,0
        Print score
        Use BitMap 1-db
        BitMapOutput 1-db
        Locate 24,37
        Print coinbon," "
        f=1
    EndIf

    ;free man??
    If score=>nextfree
        Sound 8,4
        lives+1
        nextfree+10000
        Use BitMap 2
        BitMapOutput 2
        Locate 18,0
        Colour 1
        If lives<=9
            Print lives," "     ;00
        Else
            Print "9+"
        EndIf
    EndIf

    If sfx>0 Then sfx-1
Until f=0
Sound 7,1


doneel:
aa:VWait:If Joyb(1)=0 Then Goto aa
bb:VWait:If Joyb(1)<>0 Then Goto bb

UnBuffer 0
UnBuffer 1
Goto STARTLEVEL

;********************************************************
.GAMEOVER
UnBuffer 0
UnBuffer 1
Use Slice 0

VWait
Use BitMap 0
Show 1
Gosub drawmap
VWait
Use BitMap 1
Show 0
Gosub drawmap

xs=Sgn(0-sx)*2
ys=Sgn(210-sy)*2

sx=Int(Int(sx)/2)*2
sy=Int((sy)/2)*2

Repeat
    VWait
    Show 0,sx,sy
    f=0
    If sx<>0 Then sx=sx+xs:f=-1
    If sy<>210 Then sy=sy+ys:f=-1

    If f<>0
        f=0
        If sx<>0 Then sx=sx+xs:f=-1
        If sy<>210 Then sy=sy+ys:f=-1
    EndIf

Until f=0
VWait
Show 0,0,210

Use BitMap 0
Boxf 0,0,200,180,0

BitMapOutput 0

Colour 1
Locate 0,0
NPrint " GAME OVER!"

Sound 9,4

;outline in black(10)..

For y=1 To 10
    For x=1  To 89
        If Point(x,y)>0 AND Point(x,y)<>10
            If Point(x-1,y)=0
                Plot x-1,y,10
            EndIf
            If Point(x+1,y)=0
                Plot x+1,y,10
            EndIf
            If Point(x,y-1)=0
                Plot x,y-1,10
            EndIf
            If Point(x,y+1)=0
                Plot x,y+1,10
            EndIf
        EndIf
    Next
Next

GetaShape 190,0,0,90,10


Colour 1
For x=320 To 120-8 Step -4
    VWait
    Show db,0,210
    db=1-db
    Use BitMap db
    UnBuffer db

    BBlit db,190,x,272
Next

aago:
VWait
If Joyb(1)=0 Then Goto aago

bbgo:
VWait
If Joyb(1)<>0 Then Goto bbgo

Goto FRONTEND

;********************************************************
;********************************************************
.EDITOR

editor=1

Gosub drawmap

mx=0:my=0
ex=0:ey=0

cb=12

.edframe:
VWait 2
Use Slice 0
Show 0,mx*16,my*16
Use BitMap 0
UnBuffer 0

Use Slice 1
Use BitMap 2
BitMapOutput 2
Locate 0,0
Print "                                      "
Locate 0,0
Print "EX:",Int(ex)," "
Locate 8,0
Print"EY:",Int(ey)," "

Use Slice 0
Use BitMap 0
BitMapOutput 0


a$=Inkey$
If a$="=" Then cb=QWrap(cb+1,12,44)
If a$="-" Then cb=QWrap(cb-1,12,44)
If a$=" "
    editor=0
    Gosub drawmap
    Use Slice 1
    Use BitMap 2
    BitMapOutput 2
    Locate 0,0
    Print "             "
    Locate 8,0
    Print"              "

    Locate 0,0
    Colour 2
    Print "SCORE:"
    Locate 6,0
    Colour 1
    Print score     ;000000

    Locate 12,0
    Colour 3
    Print "LIVES:"
    Locate 18,0
    Colour 1
    If lives<=9
        Print lives," "     ;00
    Else
        Print "9+"
    EndIf

    Locate 21,0
    Colour 4
    Print "COINS:"
    Locate 27,0
    Colour 1
    Print coins     ;000

    Locate 31,0
    Colour 5
    Print "TIME:"
    Locate 36,0
    Colour 1
    Print Int(time) ;000

    Use Slice 0

    Goto FRAME
EndIf

If a$="h"       ;hide
    If map(Int(ex),Int(ey))>1
        map(Int(ex),Int(ey))=-map(Int(ex),Int(ey))
        Boxf Int(ex)*16,Int(ey)*16,Int(ex)*16+15,Int(ey)*16+15,-1
    EndIf
EndIf

If a$="d"       ;delete
    map(Int(ex),Int(ey))=-1
    Boxf Int(ex)*16,Int(ey)*16,Int(ex)*16+15,Int(ey)*16+15,0
EndIf

If a$="c"
    For y=0 To 24
        For x=0 To 39
            map(x,y)=-1
        Next
    Next
    Gosub drawmap
EndIf

If a$="s"
    BitMapInput
    Show 0,0,0
    Locate 0,0
    Print  "                "
    Locate 0,0
    NPrint "LEVEL #?"
    Locate 9,0
    lvl=Edit(3)

    f$="jack/map"+Str$(lvl)+".bin"

    AMIGA
    h.l=Open_(f$,1006)
    For y=0 To 24
        For x=0 To 39
            a.b=map(x,y)
            c.l=Write_(h,&a,1)
        Next
    Next
    c.l=Close_(h)
    End
    BLITZ
EndIf

jx=Joyx(1)
jy=Joyy(1)

ex=QLimit(ex+(jx/4),0,39)
ey=QLimit(ey+(jy/4),0,24)

If Int(ex)>mx+19 AND mx<20 Then mx=mx+1
If Int(ex)<mx AND mx>0Then mx=mx-1

If Int(ey)>my+11 AND mx<24 Then my=my+1
If Int(ey)<my AND my>0Then my=my-1

If Joyb(1)=1
    Boxf Int(ex)*16,Int(ey)*16,Int(ex)*16+15,Int(ey)*16+15,0
    Blit cb,Int(ex)*16,Int(ey)*16
    map(Int(ex),Int(ey))=cb
EndIf

BBlit 0,cb,Int(ex)*16,Int(ey)*16

Goto edframe

;********************************************************
.drawmap
DisplayOff

Use BitMap 0
Cls
Use BitMap 1
Cls

For y=0 To 24
    For x=0 To 39
        If map(x,y)>-1
            Use BitMap 0
            Blit map(x,y),x*16,y*16
            Use BitMap 1
            Blit map(x,y),x*16,y*16
        EndIf
        If map(x,y)<-1 AND editor=1
            Use BitMap 0
            Blit -map(x,y),x*16,y*16
            Boxf x*16,y*16,x*16+15,y*16+15,-1
            Use BitMap 1
            Blit -map(x,y),x*16,y*16
            Boxf x*16,y*16,x*16+15,y*16+15,-1
        EndIf
    Next
Next
DisplayOn
Return
;********************************************************
